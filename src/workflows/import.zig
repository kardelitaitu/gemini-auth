const std = @import("std");
const cli = @import("../cli/root.zig");
const registry = @import("../registry/root.zig");
const account_names = @import("account_names.zig");

const loadSingleFileImportAuthInfo = account_names.loadSingleFileImportAuthInfo;
const refreshAccountNamesAfterImport = account_names.refreshAccountNamesAfterImport;
const defaultAccountFetcher = account_names.defaultAccountFetcher;

pub fn handleImport(allocator: std.mem.Allocator, gemini_home: []const u8, opts: cli.types.ImportOptions) !void {
    if (opts.purge) {
        var report = try registry.purgeRegistryFromImportSource(allocator, gemini_home, opts.auth_path, opts.alias);
        defer report.deinit(allocator);
        try cli.output.printImportReport(&report);
        if (report.failure) |err| return err;
        return;
    }

    var reg = try registry.loadRegistry(allocator, gemini_home);
    defer reg.deinit(allocator);
    var report = switch (opts.source) {
        .standard => try registry.importAuthPath(allocator, gemini_home, &reg, opts.auth_path.?, opts.alias),
        .cpa => try registry.importCpaPath(allocator, gemini_home, &reg, opts.auth_path, opts.alias),
    };
    defer report.deinit(allocator);
    if (report.appliedCount() > 0) {
        if (report.render_kind == .single_file) {
            var imported_info = try loadSingleFileImportAuthInfo(allocator, opts);
            defer if (imported_info) |*info| info.deinit(allocator);
            _ = try refreshAccountNamesAfterImport(
                allocator,
                &reg,
                opts.purge,
                report.render_kind,
                if (imported_info) |*info| info else null,
                defaultAccountFetcher,
            );
        }
        try registry.saveRegistry(allocator, gemini_home, &reg);
    }
    try cli.output.printImportReport(&report);
    if (report.failure) |err| return err;
}
